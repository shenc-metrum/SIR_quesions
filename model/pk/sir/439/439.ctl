$PROBLEM SIR 106-104 + COV-effects(EGFR, AGE) on CL

$INPUT C NUM ID TIME SEQ CMT EVID AMT DV AGE WT HT EGFR ALB BMI SEX AAG
       SCR AST ALT CP TAFD TAD LDOS MDV BLQ PHASE OID

$DATA ../../../../data/derived/analysis3.csv IGNORE=(C='C', BLQ=1)

$SUBROUTINE ADVAN4 TRANS4

$PK
 
;log transformed PK parms
 
V2WT = LOG(WT/70)
CLWT = LOG(WT/70)*0.75
CLEGFR = LOG(EGFR/90)*THETA(6)
CLAGE = LOG(AGE/35)*THETA(7)
V3WT = LOG(WT/70)
QWT  = LOG(WT/70)*0.75
CLALB = LOG(ALB/4.5)*THETA(8)


KA   = EXP(THETA(1)+ETA(1))
V2   = EXP(THETA(2)+V2WT+ETA(2))
CL   = EXP(THETA(3)+CLWT+CLEGFR+CLAGE+CLALB+ETA(3))
V3   = EXP(THETA(4)+V3WT)
Q    = EXP(THETA(5)+QWT) 

S2 = V2/1000 ; dose in mcg, conc in mcg/mL

$ERROR
IPRED = F 
Y=IPRED*(1+EPS(1))

$THETA  ; log values
(0.493162007258106)   ;  1 KA (1/hr) - 1.5
(4.16060814145944)   ;  2 V2 (L) - 60
(1.18180499121561)   ;  3 CL (L/hr) - 3.5
(4.21194861409823)   ;  4 V3 (L) - 70
(1.26998270628482)   ;  5 Q  (L/hr) - 4
(0.473252300790478)   ;  6 CLEGFR~CL ()
(-0.0823475302002625)   ;  7 AGE~CL ()
(0.474713610060777)   ;  8 ALB~CL ()

$OMEGA BLOCK(3)
0.28027657161555                                 ;ETA(KA)
0.0828163442412183 0.0948017648927774                  ;ETA(V2)
0.147848396272558 0.0788024855575875 0.111411093808406   ;ETA(CL)

$SIGMA
0.040156083771749     ; 1 pro error

$EST MAXEVAL=0 METHOD=1 INTER SIGL=6 NSIG=3 PRINT=1