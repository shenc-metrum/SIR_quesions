$PROBLEM SIR 106-104 + COV-effects(EGFR, AGE) on CL

$INPUT C NUM ID TIME SEQ CMT EVID AMT DV AGE WT HT EGFR ALB BMI SEX AAG
       SCR AST ALT CP TAFD TAD LDOS MDV BLQ PHASE OID

$DATA ../../../../data/derived/analysis3.csv IGNORE=(C='C', BLQ=1)

$SUBROUTINE ADVAN4 TRANS4

$PK
 
;log transformed PK parms
 
V2WT = LOG(WT/70)
CLWT = LOG(WT/70)*0.75
CLEGFR = LOG(EGFR/90)*THETA(6)
CLAGE = LOG(AGE/35)*THETA(7)
V3WT = LOG(WT/70)
QWT  = LOG(WT/70)*0.75
CLALB = LOG(ALB/4.5)*THETA(8)


KA   = EXP(THETA(1)+ETA(1))
V2   = EXP(THETA(2)+V2WT+ETA(2))
CL   = EXP(THETA(3)+CLWT+CLEGFR+CLAGE+CLALB+ETA(3))
V3   = EXP(THETA(4)+V3WT)
Q    = EXP(THETA(5)+QWT) 

S2 = V2/1000 ; dose in mcg, conc in mcg/mL

$ERROR
IPRED = F 
Y=IPRED*(1+EPS(1))

$THETA  ; log values
(0.499457868816027)   ;  1 KA (1/hr) - 1.5
(4.10272687906036)   ;  2 V2 (L) - 60
(1.1277999748358)   ;  3 CL (L/hr) - 3.5
(4.18718785114819)   ;  4 V3 (L) - 70
(1.2752716533845)   ;  5 Q  (L/hr) - 4
(0.423362292591908)   ;  6 CLEGFR~CL ()
(0.0241195893589222)   ;  7 AGE~CL ()
(0.383867919806127)   ;  8 ALB~CL ()

$OMEGA BLOCK(3)
0.304878828948523                                 ;ETA(KA)
0.0847814803456102 0.0841082530917037                  ;ETA(V2)
0.154186933303267 0.0748772537101491 0.127164537971118   ;ETA(CL)

$SIGMA
0.0394402818323418     ; 1 pro error

$EST MAXEVAL=0 METHOD=1 INTER SIGL=6 NSIG=3 PRINT=1