$PROBLEM SIR 106-104 + COV-effects(EGFR, AGE) on CL

$INPUT C NUM ID TIME SEQ CMT EVID AMT DV AGE WT HT EGFR ALB BMI SEX AAG
       SCR AST ALT CP TAFD TAD LDOS MDV BLQ PHASE OID

$DATA ../../../../data/derived/analysis3.csv IGNORE=(C='C', BLQ=1)

$SUBROUTINE ADVAN4 TRANS4

$PK
 
;log transformed PK parms
 
V2WT = LOG(WT/70)
CLWT = LOG(WT/70)*0.75
CLEGFR = LOG(EGFR/90)*THETA(6)
CLAGE = LOG(AGE/35)*THETA(7)
V3WT = LOG(WT/70)
QWT  = LOG(WT/70)*0.75
CLALB = LOG(ALB/4.5)*THETA(8)


KA   = EXP(THETA(1)+ETA(1))
V2   = EXP(THETA(2)+V2WT+ETA(2))
CL   = EXP(THETA(3)+CLWT+CLEGFR+CLAGE+CLALB+ETA(3))
V3   = EXP(THETA(4)+V3WT)
Q    = EXP(THETA(5)+QWT) 

S2 = V2/1000 ; dose in mcg, conc in mcg/mL

$ERROR
IPRED = F 
Y=IPRED*(1+EPS(1))

$THETA  ; log values
(0.453359251772453)   ;  1 KA (1/hr) - 1.5
(4.11925471509835)   ;  2 V2 (L) - 60
(1.18804635372617)   ;  3 CL (L/hr) - 3.5
(4.21635306939202)   ;  4 V3 (L) - 70
(1.2926631695422)   ;  5 Q  (L/hr) - 4
(0.529057746381435)   ;  6 CLEGFR~CL ()
(0.0112455250234301)   ;  7 AGE~CL ()
(0.451765379853822)   ;  8 ALB~CL ()

$OMEGA BLOCK(3)
0.25901586592228                                 ;ETA(KA)
0.0704495616096883 0.0817120234428392                  ;ETA(V2)
0.114969092433392 0.0691447279238487 0.111379547141978   ;ETA(CL)

$SIGMA
0.0407088214050932     ; 1 pro error

$EST MAXEVAL=0 METHOD=1 INTER SIGL=6 NSIG=3 PRINT=1