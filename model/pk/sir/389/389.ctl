$PROBLEM SIR 106-104 + COV-effects(EGFR, AGE) on CL

$INPUT C NUM ID TIME SEQ CMT EVID AMT DV AGE WT HT EGFR ALB BMI SEX AAG
       SCR AST ALT CP TAFD TAD LDOS MDV BLQ PHASE OID

$DATA ../../../../data/derived/analysis3.csv IGNORE=(C='C', BLQ=1)

$SUBROUTINE ADVAN4 TRANS4

$PK
 
;log transformed PK parms
 
V2WT = LOG(WT/70)
CLWT = LOG(WT/70)*0.75
CLEGFR = LOG(EGFR/90)*THETA(6)
CLAGE = LOG(AGE/35)*THETA(7)
V3WT = LOG(WT/70)
QWT  = LOG(WT/70)*0.75
CLALB = LOG(ALB/4.5)*THETA(8)


KA   = EXP(THETA(1)+ETA(1))
V2   = EXP(THETA(2)+V2WT+ETA(2))
CL   = EXP(THETA(3)+CLWT+CLEGFR+CLAGE+CLALB+ETA(3))
V3   = EXP(THETA(4)+V3WT)
Q    = EXP(THETA(5)+QWT) 

S2 = V2/1000 ; dose in mcg, conc in mcg/mL

$ERROR
IPRED = F 
Y=IPRED*(1+EPS(1))

$THETA  ; log values
(0.489432917105799)   ;  1 KA (1/hr) - 1.5
(4.13224377880725)   ;  2 V2 (L) - 60
(1.19813571343323)   ;  3 CL (L/hr) - 3.5
(4.21258838584026)   ;  4 V3 (L) - 70
(1.29354348682178)   ;  5 Q  (L/hr) - 4
(0.500696900917304)   ;  6 CLEGFR~CL ()
(-0.102688640553648)   ;  7 AGE~CL ()
(0.52638348240602)   ;  8 ALB~CL ()

$OMEGA BLOCK(3)
0.146630716203438                                 ;ETA(KA)
0.0604933461901137 0.0879185564702593                  ;ETA(V2)
0.130580854423436 0.0776142792672381 0.130599306889221   ;ETA(CL)

$SIGMA
0.0423479858273604     ; 1 pro error

$EST MAXEVAL=0 METHOD=1 INTER SIGL=6 NSIG=3 PRINT=1