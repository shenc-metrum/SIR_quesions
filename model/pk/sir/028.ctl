$PROBLEM SIR 106-104 + COV-effects(EGFR, AGE) on CL

$INPUT C NUM ID TIME SEQ CMT EVID AMT DV AGE WT HT EGFR ALB BMI SEX AAG
       SCR AST ALT CP TAFD TAD LDOS MDV BLQ PHASE OID

$DATA ../../../../data/derived/analysis3.csv IGNORE=(C='C', BLQ=1)

$SUBROUTINE ADVAN4 TRANS4

$PK
 
;log transformed PK parms
 
V2WT = LOG(WT/70)
CLWT = LOG(WT/70)*0.75
CLEGFR = LOG(EGFR/90)*THETA(6)
CLAGE = LOG(AGE/35)*THETA(7)
V3WT = LOG(WT/70)
QWT  = LOG(WT/70)*0.75
CLALB = LOG(ALB/4.5)*THETA(8)


KA   = EXP(THETA(1)+ETA(1))
V2   = EXP(THETA(2)+V2WT+ETA(2))
CL   = EXP(THETA(3)+CLWT+CLEGFR+CLAGE+CLALB+ETA(3))
V3   = EXP(THETA(4)+V3WT)
Q    = EXP(THETA(5)+QWT) 

S2 = V2/1000 ; dose in mcg, conc in mcg/mL

$ERROR
IPRED = F 
Y=IPRED*(1+EPS(1))

$THETA  ; log values
(0.507107497935831)   ;  1 KA (1/hr) - 1.5
(4.13090848792977)   ;  2 V2 (L) - 60
(1.19116436982705)   ;  3 CL (L/hr) - 3.5
(4.2105362041391)   ;  4 V3 (L) - 70
(1.27041604549373)   ;  5 Q  (L/hr) - 4
(0.513505653308237)   ;  6 CLEGFR~CL ()
(-0.0221317297621071)   ;  7 AGE~CL ()
(0.496907984797817)   ;  8 ALB~CL ()

$OMEGA BLOCK(3)
0.219363064938385                                 ;ETA(KA)
0.0644419360477866 0.0850468871046741                  ;ETA(V2)
0.110129122722441 0.0741535512208577 0.116760651919515   ;ETA(CL)

$SIGMA
0.041126799457682     ; 1 pro error

$EST MAXEVAL=0 METHOD=1 INTER SIGL=6 NSIG=3 PRINT=1