$PROBLEM SIR 106-104 + COV-effects(EGFR, AGE) on CL

$INPUT C NUM ID TIME SEQ CMT EVID AMT DV AGE WT HT EGFR ALB BMI SEX AAG
       SCR AST ALT CP TAFD TAD LDOS MDV BLQ PHASE OID

$DATA ../../../../data/derived/analysis3.csv IGNORE=(C='C', BLQ=1)

$SUBROUTINE ADVAN4 TRANS4

$PK
 
;log transformed PK parms
 
V2WT = LOG(WT/70)
CLWT = LOG(WT/70)*0.75
CLEGFR = LOG(EGFR/90)*THETA(6)
CLAGE = LOG(AGE/35)*THETA(7)
V3WT = LOG(WT/70)
QWT  = LOG(WT/70)*0.75
CLALB = LOG(ALB/4.5)*THETA(8)


KA   = EXP(THETA(1)+ETA(1))
V2   = EXP(THETA(2)+V2WT+ETA(2))
CL   = EXP(THETA(3)+CLWT+CLEGFR+CLAGE+CLALB+ETA(3))
V3   = EXP(THETA(4)+V3WT)
Q    = EXP(THETA(5)+QWT) 

S2 = V2/1000 ; dose in mcg, conc in mcg/mL

$ERROR
IPRED = F 
Y=IPRED*(1+EPS(1))

$THETA  ; log values
(0.353284194364688)   ;  1 KA (1/hr) - 1.5
(4.1133026973321)   ;  2 V2 (L) - 60
(1.15960080493972)   ;  3 CL (L/hr) - 3.5
(4.22476601663525)   ;  4 V3 (L) - 70
(1.3122529226226)   ;  5 Q  (L/hr) - 4
(0.454107960839373)   ;  6 CLEGFR~CL ()
(0.0183995588419291)   ;  7 AGE~CL ()
(0.4387354469276)   ;  8 ALB~CL ()

$OMEGA BLOCK(3)
0.230300171225535                                 ;ETA(KA)
0.0826641979570007 0.0938031844821295                  ;ETA(V2)
0.141111021144914 0.0745115295515947 0.11590590662556   ;ETA(CL)

$SIGMA
0.0388694701354754     ; 1 pro error

$EST MAXEVAL=0 METHOD=1 INTER SIGL=6 NSIG=3 PRINT=1